plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.jetbrains.kotlin.jvm' version '2.1.20'
    id 'org.jetbrains.kotlin.plugin.lombok' version '2.1.20'
}

group = 'no.fintlabs'
version = '0.0.1-SNAPSHOT'

java { sourceCompatibility = '21'}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    maven {
        url "https://repo.fintlabs.no/releases"
    }
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-configuration-processor'

    implementation "no.fint:fint-utdanning-resource-model-java:${fintVersion}"
    implementation "no.fint:fint-administrasjon-resource-model-java:${fintVersion}"
    implementation "no.fint:fint-personvern-resource-model-java:${fintVersion}"
    implementation "no.fint:fint-okonomi-resource-model-java:${fintVersion}"
    implementation "no.fint:fint-ressurs-resource-model-java:${fintVersion}"
    implementation "no.fint:fint-arkiv-resource-model-java:${fintVersion}"

    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'no.fintlabs:fint-kafka:3.0.0-rc-1'

    implementation 'no.fintlabs:fint-antlr:1.1.1'
    implementation 'no.fintlabs:fint-core-cache:2.0.0-rc-5'
    implementation 'no.fintlabs:fint-core-relations:0.0.4'
    implementation 'no.fintlabs:fint-core-infra-models:2.1.0'
    implementation 'no.fintlabs:fint-core-status-models:1.0.0'
    implementation 'no.fintlabs:core-resource-server:3.0.0-rc-7'
    implementation 'no.fintlabs:fint-core-autorelation-lib:3.1.0'

    implementation 'com.github.ben-manes.caffeine:caffeine:2.8.8'
    implementation 'org.reflections:reflections:0.10.2'
    implementation 'org.apache.commons:commons-text:1.3'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation "no.fint:fint-utdanning-resource-model-java:${fintTestVersion}"
    testImplementation "no.fint:fint-administrasjon-resource-model-java:${fintTestVersion}"
    testImplementation "no.fint:fint-personvern-resource-model-java:${fintTestVersion}"
    testImplementation "no.fint:fint-okonomi-resource-model-java:${fintTestVersion}"
    testImplementation "no.fint:fint-ressurs-resource-model-java:${fintTestVersion}"
    testImplementation "no.fint:fint-arkiv-resource-model-java:${fintTestVersion}"

    testImplementation("io.mockk:mockk:1.13.13")
    testImplementation("org.jetbrains.kotlin:kotlin-test")
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

plugins.withId('org.jetbrains.kotlin.jvm') {
    sourceSets {
        test {
            kotlin.srcDirs 'src/test/unit/java', 'src/test/integration/java'
        }
    }
}

kotlin { jvmToolchain(21) }

sourceSets {
    def resourceDir = 'src/test/resources'

    unit {
        java.srcDir 'src/test/unit/java'
        resources.srcDir resourceDir
        compileClasspath += sourceSets.main.output + configurations.testCompileClasspath
        runtimeClasspath += output + compileClasspath
    }

    integration {
        java.srcDir 'src/test/integration/java'
        resources.srcDir resourceDir
        compileClasspath += sourceSets.main.output + configurations.testCompileClasspath
        runtimeClasspath += output + compileClasspath
    }
}

configurations {
    unitImplementation.extendsFrom testImplementation
    unitRuntimeOnly.extendsFrom testRuntimeOnly

    integrationImplementation.extendsFrom testImplementation
    integrationRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    maxParallelForks = 1
    systemProperty 'junit.jupiter.execution.parallel.enabled', 'false'
}

tasks.register('unit', Test) {
    description = 'Runs unit tests.'
    group = 'verification'
    testClassesDirs = sourceSets.unit.output.classesDirs
    classpath = sourceSets.unit.runtimeClasspath
}

tasks.register('integration', Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
}

tasks.named('check') {
    dependsOn 'unit', 'integration'
}

