plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.jetbrains.kotlin.jvm' version '2.1.20'
    id 'org.jetbrains.kotlin.plugin.lombok' version '2.1.20'
    id 'org.jetbrains.kotlin.plugin.spring' version '2.1.20'
}

group = 'no.fintlabs'
version = '0.0.1-SNAPSHOT'

java { sourceCompatibility = '21'}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    maven {
        url "https://repo.fintlabs.no/releases"
    }
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-configuration-processor'

    implementation "no.novari:fint-utdanning-resource-model-java:${fintVersion}"
    implementation "no.novari:fint-administrasjon-resource-model-java:${fintVersion}"
    implementation "no.novari:fint-personvern-resource-model-java:${fintVersion}"
    implementation "no.novari:fint-okonomi-resource-model-java:${fintVersion}"
    implementation "no.novari:fint-ressurs-resource-model-java:${fintVersion}"
    implementation "no.novari:fint-arkiv-resource-model-java:${fintVersion}"

    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'no.fintlabs:fint-kafka:3.2.0-rc-3'

    implementation 'no.fintlabs:fint-antlr:1.1.1'
    implementation 'no.fintlabs:fint-core-cache:2.0.0-rc-6'
    implementation 'no.novari:fint-core-relations:1.0.0-rc.1'
    implementation 'no.fintlabs:fint-core-infra-models:2.1.0'
    implementation 'no.fintlabs:fint-core-status-models:1.0.0'
    implementation 'no.fintlabs:core-resource-server:3.0.0-rc-7'
    implementation 'no.novari:fint-core-metamodel:3.0.0-SNAPSHOT'

    implementation("io.github.microutils:kotlin-logging-jvm:3.0.5")
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin")
    implementation 'com.github.ben-manes.caffeine:caffeine:2.8.8'
    implementation 'org.reflections:reflections:0.10.2'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation("io.mockk:mockk:1.13.13")
    testImplementation 'org.awaitility:awaitility:4.3.0'
    testImplementation("org.jetbrains.kotlin:kotlin-test")
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

kotlin { jvmToolchain(21) }

testing {
    suites {
        integrationTest(JvmTestSuite) {
            dependencies {
                implementation project()

                // Copy each dependency from testImplementation
                configurations.testImplementation.allDependencies.each {
                    implementation(it)
                }

                configurations.testRuntimeOnly.allDependencies.each {
                    runtimeOnly(it)
                }

            }

            targets {
                configureEach {
                    testTask.configure {
                        shouldRunAfter(test) // Run after unit tests
                        useJUnitPlatform()
                    }
                }
            }
        }
    }
}

tasks.named('check') {
    dependsOn testing.suites.integrationTest
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    maxParallelForks = 1
    systemProperty 'junit.jupiter.execution.parallel.enabled', 'false'
}
